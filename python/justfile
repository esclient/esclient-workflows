PORT           := env_var('PORT')
SERVICE_NAME   := env_var('SERVICE_NAME')
TMP_DIR        := env_var('TMP_DIR')
OUT_DIR        := env_var('OUT_DIR')
PROTO_TAG      := env_var('PROTO_TAG')
PROTO_NAME     := env_var('PROTO_NAME')

is_windows := os() == 'windows'

MKDIR := if is_windows { 'powershell -Command "New-Item -ItemType Directory -Force -Path"' } else { 'mkdir -p' }
RM := if is_windows { 'powershell -NoProfile -Command "Remove-Item -Path ' + TMP_DIR + ' -Recurse -Force"' } else { 'rm -rf ' + TMP_DIR }
DOWN := if is_windows { 'powershell -Command "Invoke-WebRequest -Uri"' } else { 'wget' }
DOWN_OUT := if is_windows { '-OutFile' } else { '-O' }
PY_FIX_IMPORTS := if is_windows {
  'powershell -Command "& { Get-ChildItem -Path \'" + OUT_DIR + "\' -Filter \"*_pb2_grpc.py\" | ForEach-Object { (Get-Content $($_.FullName)) -replace \"^import (.*_pb2)\", \"from . import $1\" | Set-Content -Path $($_.FullName) -Encoding UTF8 } }"'
} else {
  'bash -c "for f in ' + OUT_DIR + '/*_pb2_grpc.py; do [ -f \"$f\" ] && sed -i \"s/^import \(.*_pb2\)/from . import \\1/\" \"$f\"; done"'
}

ok := 'echo All checks passed! âœ…'

default:
    @just --list

clean:
    {{RM}}

fetch-proto:
    {{MKDIR}} "{{TMP_DIR}}"
    {{DOWN}} "https://raw.githubusercontent.com/esclient/protos/{{PROTO_TAG}}/{{PROTO_NAME}}" {{DOWN_OUT}} "{{TMP_DIR}}/{{PROTO_NAME}}"

docker-build:
    docker build --build-arg PORT={{PORT}} -t {{SERVICE_NAME}} .

run: docker-build
    docker run --rm -it \
      --env-file .env \
      -p {{PORT}}:{{PORT}} \
      -v {{invocation_directory()}}:/app \
      -e WATCHFILES_FORCE_POLLING=true \
      {{SERVICE_NAME}}

gen-stubs: fetch-proto
    {{MKDIR}} "{{OUT_DIR}}"
    pdm run python -m grpc_tools.protoc \
      --proto_path="{{TMP_DIR}}" \
      --python_out="{{OUT_DIR}}" \
      --grpc_python_out="{{OUT_DIR}}" \
      --pyi_out="{{OUT_DIR}}" \
      "{{TMP_DIR}}/{{PROTO_NAME}}"
    {{PY_FIX_IMPORTS}}

update: gen-stubs clean

format:
    black .
    isort .
    ruff check . --fix

lint:
    black --check .
    isort . --check --diff
    flake8 .
    ruff check .
    mypy --strict .

test:
    pytest

prepare: format lint test
    {{ok}}
